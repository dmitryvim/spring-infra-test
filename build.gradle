buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'io.freefair.lombok' version '6.1.0-m3'
    id 'java'
}

group = 'com.dmitryvim'

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.register('intTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

dependencies {
    implementation 'org.liquibase:liquibase-core:4.4.2'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.5.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.5.3'

    runtimeOnly 'org.postgresql:postgresql:42.2.23.jre7'

    testImplementation platform('org.junit:junit-bom:5.7.2')

    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'

    intTestImplementation 'org.assertj:assertj-core:3.20.2'
    intTestImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.3'
}

test {
    useJUnitPlatform()
}

intTest {
    useJUnitPlatform()
}